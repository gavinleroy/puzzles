(eval-when (:compile-toplevel :load-toplevel :execute)
  (ql:quickload '(:priority-queue :fset)))

(defpackage :day16
  (:import-from :priority-queue #:make-pqueue #:pqueue-push #:pqueue-pop #:pqueue-empty-p)
  (:use :cl))

(in-package :day16)

;; --- Day 16: Reindeer Maze ---
;; It's time again for the Reindeer Olympics! This year, the big event is the Reindeer Maze, where the Reindeer compete for the lowest score.

;; You and The Historians arrive to search for the Chief right as the event is about to start. It wouldn't hurt to watch a little, right?

;; The Rmindeer start on the Start Tile (marked S) facing East and need to reach the End Tile (marked E). They can move forward one tile at a time (increasing their score by 1 point), but never into a wall (#). They can also rotate clockwise or counterclockwise 90 degrees at a time (increasing their score by 1000 points).

;; To figure out the best place to sit, you start by grabbing a map (your puzzle input) from a nearby kiosk. For example:

;; ###############
;; #.......#....E#
;; #.#.###.#.###.#
;; #.....#.#...#.#
;; #.###.#####.#.#
;; #.#.#.......#.#
;; #.#.#####.###.#
;; #...........#.#
;; ###.#.#####.#.#
;; #...#.....#.#.#
;; #.#.#.###.#.#.#
;; #.....#...#.#.#
;; #.###.#.#.#.#.#
;; #S..#.....#...#
;; ###############
;; There are many paths through this maze, but taking any of the best paths would incur a score of only 7036. This can be achieved by taking a total of 36 steps forward and turning 90 degrees a total of 7 times:


;; ###############
;; #.......#....E#
;; #.#.###.#.###^#
;; #.....#.#...#^#
;; #.###.#####.#^#
;; #.#.#.......#^#
;; #.#.#####.###^#
;; #..>>>>>>>>v#^#
;; ###^#.#####v#^#
;; #>>^#.....#v#^#
;; #^#.#.###.#v#^#
;; #^....#...#v#^#
;; #^###.#.#.#v#^#
;; #S..#.....#>>^#
;; ###############
;; Here's a second example:

;; #################
;; #...#...#...#..E#
;; #.#.#.#.#.#.#.#.#
;; #.#.#.#...#...#.#
;; #.#.#.#.###.#.#.#
;; #...#.#.#.....#.#
;; #.#.#.#.#.#####.#
;; #.#...#.#.#.....#
;; #.#.#####.#.###.#
;; #.#.#.......#...#
;; #.#.###.#####.###
;; #.#.#...#.....#.#
;; #.#.#.#####.###.#
;; #.#.#.........#.#
;; #.#.#.#########.#
;; #S#.............#
;; #################
;; In this maze, the best paths cost 11048 points; following one such path would look like this:

;; #################
;; #...#...#...#..E#
;; #.#.#.#.#.#.#.#^#
;; #.#.#.#...#...#^#
;; #.#.#.#.###.#.#^#
;; #>>v#.#.#.....#^#
;; #^#v#.#.#.#####^#
;; #^#v..#.#.#>>>>^#
;; #^#v#####.#^###.#
;; #^#v#..>>>>^#...#
;; #^#v###^#####.###
;; #^#v#>>^#.....#.#
;; #^#v#^#####.###.#
;; #^#v#^........#.#
;; #^#v#^#########.#
;; #S#>>^..........#
;; #################
;; Note that the path shown above includes one 90 degree turn as the very first move, rotating the Reindeer from facing East to facing North.

;; Analyze your map carefully. What is the lowest score a Reindeer could possibly get?

(defun read-input (filename)
  (flet ((concat-all (ls) (format nil "狺祗┅麸蝻鳝磲觑磴ǐí磴颟悌麸鏖漪瑭扉篝ㄦ祜矧鏖漪瑭盹鏖漪瑭┅眭祠轲戾鲠祯瀛忾钿眚磴轸鏖翳镳孱骈戾篝蝈犴骈戾钺礤祜镳骘扉铄蝈徜扉铄篝蝈犴铋飑骘骝镯麒殪扉铄磲轫辁轭轭麸眚磲轫辁轭戾铉翳扉铄轭麸磴泔祆邈扉铄轭麸轸骈钺祆蝈趱蝾鲠祯弩眚磴轸┅┅戾舄èㄣ镱汜舡犰轸┅篝狎麸痫箝糸镱＼螬磴┅ㄥ钿麸痫箝糸镱＼螬磴┅ㄧ弭翦灬礅溽悌麒孱ㄡ钿眚磴┅ㄡ蝈麸蝻鳝磲觑磴┅┅┅鲠祯弩篝狎孱珏趑弪┅┅ㄤ彐泔铙翎铘溟蝮В苯è暴ū癌ò暴ō癌１）ㄤ彐躅戾骠ㄤ轵ㄣ滗潋溟颟ㄤ彐躅蜷玷ㄤ轵ㄣ潋溟颟ㄤ彐躅痫螳鸨鸩扉篝ǐㄦ轵篝鸨ㄦ轵篝鸩┅ǐ箦泔钿鸨箦泔钿鸩┅┅ㄤ彐躅铄殓桠矧珏舂祜镳鏖翳洎骘滗轭扉篝戾骠洎蜷玷洎骘泔篝轭扉篝卑氨卑氨骘漯痫螳ㄦ轵篝滗┅骘狒ㄦ躅汜祆珏ㄦ轵篝漯箦泔钿漯┅麒孱狒躅戾篌ㄣ栳蚪狒＼）泔祆邈扉篝扉篝漯滗泔篝┅ㄤ彐躅溟觌篝蜥ㄧ弭箫躜沐戾舄è溟篝磲脲栳箬翎忪呼弩＇羼踽飑痱弼磲脲栳箬翎忪呼弩＇羼踽飑磲脲瘃蹂蹂＇缉篝狎扉篝箫躜沐溟蝮┅瘃蹂蹂瘐箬篝狎箦翩ㄧ弭栳箬篝狎溟篝癌瘵祜镳躅糸瘃蹂蹂屙痿瘵骘瘃蹂蹂痫瘵滹祜镳骘泔篝轭铄殓桠矧珏舂骘犰ǐㄧ弭栳箬溟篝泔篝骘鲣轶ㄧ弭栳箬溟篝殒矧铒鲣轶舂犰鲣轶舂滹箦翩ㄧ弭栳箬溟篝犰舂瘃蹂蹂瘐箬犰瘵箦翩ㄧ弭栳箬痱弼扉篝酴屐箦殒犰鲣轶舂滹瘃蹂蹂瘐箬犰瘵箦翩ㄧ弭栳箬痱弼ㄣ镱ㄧ弭栳箬痱弼┅┅骈钺祆蝈趱蝾鲠祯弩溟篝痱弼┅┅ㄤ彐躅疳螋ㄅ溟篝祜镳蝈疱狒骘镱溟蝮骘狒ㄧ弭栳箬扉篝洎溟篝麒孱狒黹铋黹狒┅换嗅螋憎换物翳狒秕腩秣麒狒翳忮篝疳翳祜镫扉脲秕汜骈珲蝈秕翳忮篝箴雉麸箝舢换碰弪铒瞽麽祆糸戾ㄓ矧农轶羼蹰痧邃鏖翳痨徙弩麸箝犰镱翳邃珏镦翳糸戾阻殪溴翦蝽轭轭麒殂镦翳弩糸戾黠蹯忮翳忮篝箴雉麸箝溴疱钿镱麒镬怩钽镦驷泗矧ㄨ秣泔礞矧翎忪翳箦狒狎瀣栾驷狩狴翳忉翳蝻镯狎瀣麒弭桢翳弪濮痖祆狎忪镢腴铉秕鲩鬻弭惝┈翳盹篝轫痫螋犷驷泗矧轶麒弭桢翳糸戾轶镱镱镦翳忮篝疳翳翳蝻蹒翳磲瀹涉秕箝箫礤麒弪屐箦秕т黹篌犰翳徙糸镱换语秕ъ铄邃麸溴翦蝽轭麒殂糸戾狎疳螋镦犷忮篝疳翳翳蝻蹒翳磲瀣轭沆蹁轭翳犷糸戾螽换深翳骈蝮屮犴痨瀣翳弪狎吹糸戾磲螂邃烯翳狒狎疳螋镦狒戾狍镱镦翳鲠蜷秕忮篝疳翳翳蝻蹒翳磲搴换＃＃＃＃＃＃＃换．．希换．．＃．．＃Ｏ换．．．Ｏ换．＃．＃＃．Ｏ换．．．Ｏ换．．＃＃．＃Ｏ换．舷舷舷舷希希换＃ＯＯ＃＃ＯＯ换Ｏ舷ＯＯＯ换ＯＯＯ＃．ＯＯ换Ｏ舷舷．ＯＯ换Ｏ＃．．．ＯＯ换Ｏ．Ｏ舷换＃＃＃＃＃＃＃换深翳箦泔钿屮犴痨瀣翳弪狎洞糸戾翳狒狎疳螋镦狒戾狍镱镦翳忮篝疳翳蠛换＃＃＃＃＃＃＃＃换．．．．希换．．．．．．．Ｏ换．．．．．Ｏ换．．．．＃．．Ｏ换Ｏ舷．．．Ｏ换ＯＯ．．．＃＃Ｏ换ＯＯ．．Ｏ舷舷换ＯＯ＃＃．Ｏ＃Ｏ换ＯＯ．舷舷希舷希换ＯＯ＃Ｏ＃＃Ｏ＃换ＯＯＯ舷．舷希换ＯＯＯ＃＃Ｏ＃．换ＯＯＯ舷舷舷．换ＯＯＯ＃＃＃＃．换ＯＯ舷换＃＃＃＃＃＃＃＃换换令犰秕磲骢螋桢虍蕊磲铢糸戾狎疳螋镦狒戾狍镱镦翳忮篝疳翳翳蝻蹒翳磲蹇ㄤ彐躅趄狯弪箦疳翳痱弼蝻雉螬戾è麸鲩箝ㄦ箦艉泔铞弪ф箦艉箦蝻雉螬痨徙弩ㄦ箦艉屙痿箦舂┅祜镳躅糸ㄦ箦艉屙痿麸鲩箝舂滹箦翩痨徙弩ㄦ箦艉躅轱痨徙弩ㄦ箦艉轫徵＇骈蝮麸鲩箝舂麸鲩箝ㄦ箦艉蝈漉沐灬礅溽ㄡ沣瞟蝈漉沐＇骟弭瑚轸ㄧ弭栳箬痱弼洪铋糸犰鲠祯徙悌麸鲩箝洪铋糸犰鲠祯ㄦ箦艉屙痿箦舂┅骈钺祆蝈趱蝾ㄦ箦艉箝痨徙弩┅┅ㄤ彐躅疳螋ㄅ溟篝痱弼祜镳蝈疱狒骘镱溟蝮骘扉篝洎骘泔篝ㄧ弭栳箬溟篝麒孱泔篝黹铋黹泔篝轭麸黹犷泔祆邈ㄣ镱泔篝轭麸蝈骈钺祆蝈趱蝾趄狯弪箦疳翳痱弼磲疸狎＇骈蝮蝈盹鲥殒铒灬礅溽ㄣ潋黹瞟蝈螬┅┅ㄤ彐躅磲轭ī眭祠轲戾鲠祯瀛忾钿ㄓ珏舂蝈徜轭瘐㈤铕豸溽翎眭祠轲戾鲠祯瀛忾钿ㄤ轶痱弼ㄤ殛塍趄珏萤ㄦ矧磲⑿狎焙狺ア疳螋溟篝┅ㄦ矧磲⑿狎埠狺ア疳螋溟篝痱弼┅┅